# emacs tramp
[ ! -n "$INSIDE_EMACS" ] && [ "$TERM" = dumb ] && { unsetopt zle && PS1='%m$ ' && return; }

# get man, git log, etc. not to use more/less in an Emacs shell
if [ -n "$INSIDE_EMACS" ]
then
  export MANPAGER=cat
  export PAGER=cat
  export TERM=ansi
  export PS1='%(0?,$(shrink_path -f)> ,%B%F{red}%? $(shrink_path -f)> %f%b)'
  grep --version | grep GNU > /dev/null && alias grep='command grep --color=auto'
  type gls > /dev/null && alias ls='command gls --color=auto'
  diff --version | grep GNU > /dev/null && alias diff='command diff --color=auto'
  case "$0" in
  *zsh)
      unset    zle_bracketed_paste
      unsetopt ZLE CORRECT
      ;;
  esac
fi

# make grep, ls, and diff colorful
alias grep='grep --color=auto'
alias ls='command ls -Fh --color=auto'
alias diff='diff --color=auto'

# a zsh reimplementation of the fish prompt
shrink_path () {
        setopt localoptions
        setopt rc_quotes null_glob

        typeset -i lastfull=0
        typeset -i short=0
        typeset -i tilde=0
        typeset -i named=0

        if zstyle -t ':prompt:shrink_path' fish; then
                lastfull=1
                short=1
                tilde=1
        fi
        if zstyle -t ':prompt:shrink_path' nameddirs; then
                tilde=1
                named=1
        fi
        zstyle -t ':prompt:shrink_path' last && lastfull=1
        zstyle -t ':prompt:shrink_path' short && short=1
        zstyle -t ':prompt:shrink_path' tilde && tilde=1

        while [[ $1 == -* ]]; do
                case $1 in
                        -f|--fish)
                                lastfull=1
                                short=1
                                tilde=1
                        ;;
                        -h|--help)
                                print 'Usage: shrink_path [-f -l -s -t] [directory]'
                                print ' -f, --fish      fish-simulation, like -l -s -t'
                                print ' -l, --last      Print the last directory''s full name'
                                print ' -s, --short     Truncate directory names to the first character'
                                print ' -t, --tilde     Substitute ~ for the home directory'
                                print ' -T, --nameddirs Substitute named directories as well'
                                print 'The long options can also be set via zstyle, like'
                                print '  zstyle :prompt:shrink_path fish yes'
                                return 0
                        ;;
                        -l|--last) lastfull=1 ;;
                        -s|--short) short=1 ;;
                        -t|--tilde) tilde=1 ;;
                        -T|--nameddirs)
                                tilde=1
                                named=1
                        ;;
                esac
                shift
        done

        typeset -a tree expn
        typeset result part dir=${1-$PWD}
        typeset -i i

        [[ -d $dir ]] || return 0

        if (( named )) {
                for part in ${(k)nameddirs}; {
                        [[ $dir == ${nameddirs[$part]}(/*|) ]] && dir=${dir/#${nameddirs[$part]}/\~$part}
                }
        }
        (( tilde )) && dir=${dir/#$HOME/\~}
        tree=(${(s:/:)dir})
        (
                if [[ $tree[1] == \~* ]] {
                        cd -q ${~tree[1]}
                        result=$tree[1]
                        shift tree
                } else {
                        cd -q /
                }
                for dir in $tree; {
                        if (( lastfull && $#tree == 1 )) {
                                result+="/$tree"
                                break
                        }
                        expn=(a b)
                        part=''
                        i=0
                        until [[ (( ${#expn} == 1 )) || $dir = $expn || $i -gt 99 ]]  do
                                (( i++ ))
                                part+=$dir[$i]
                                expn=($(echo ${part}*(-/)))
                                (( short )) && break
                        done
                        result+="/$part"
                        cd -q $dir
                        shift tree
                }
                echo ${result:-/}
        )
}

# use the fishy prompt
setopt prompt_subst
export PS1='%(0?,$(shrink_path -f) > ,%B%F{red}%? $(shrink_path -f) > %f%b)'

# we need opam and path in both .zshrc and .zshenv, so that e.g. M-x
# shell has opam and ghcup on the path

# opam
eval $(opam env)

# path
export PATH=/opt/homebrew/bin:/Users/teichman/bin:/Users/teichman/.local/bin:/Users/teichman/.ghcup/bin:/Users/teichman/.stack/bin:$PATH

# get $EDITOR to behave in Emacs
export EDITOR=figure_out_editor_variable
